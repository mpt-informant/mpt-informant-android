plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jlleitschuh.gradle.ktlint' version '11.0.0'
}

android {
    namespace 'me.kofesst.android.mptinformant'
    compileSdk 33

    defaultConfig {
        applicationId "me.kofesst.android.mptinformant"
        minSdk 21
        targetSdk 33
        versionCode 5
        versionName "1.2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // Добавление значений из local.properties в BuildConfig файл
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        buildConfigField "String", "API_BASE_URL", "\"${properties.getProperty("api.baseUrl")}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lintOptions {
        disable "Instantiatable"
    }
}

ktlint {
    android = true
    debug = true
    verbose = true
    ignoreFailures = false
    disabledRules = ['no-wildcard-imports']
    reporters {
        reporter 'plain'
        reporter 'checkstyle'
        reporter 'sarif'
    }
}

dependencies {
    // Coil
    def coil_version = '2.0.0'
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"

    // Widget
    implementation 'androidx.glance:glance-appwidget:1.0.0-alpha05' // Glance widgets
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3" // Data serializer

    // Android worker with hilt providing
    implementation 'androidx.work:work-runtime-ktx:2.8.0-beta02'
    implementation 'androidx.hilt:hilt-work:1.0.0'

    // Data store
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // Accompanist
    implementation "com.google.accompanist:accompanist-pager:0.14.0" // Pager
    implementation "com.google.accompanist:accompanist-pager-indicators:0.14.0" // Pager Indicators
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.26.5-rc' // Swipe refresh

    // Retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Androidx
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // Compose
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    // Material Design
    implementation 'androidx.compose.material3:material3:1.1.0-alpha02'
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation 'androidx.compose.ui:ui-text-google-fonts:1.4.0-alpha02'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Modules
    implementation project(path: ':data')
    implementation project(path: ':domain')
}